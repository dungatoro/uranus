#!/usr/bin/raku

sub get_blocks ( Str $path ) {
    map {substr($_,3,$_.chars-6)}, slurp($path).match(/'```'(.*?)'```'/, :g)
}

multi MAIN ( 'tangle', Str $path ) {
    my @blocks = get_blocks $path;
    my @opened;

    for @blocks -> $block {
        say @opened;
        my @lines = $block.lines;
        my ($lang, $file) = @lines.shift.words;
        my $fh;
        if substr($file, 0, 1) eq '#' { next; } # skip snippets

        if $file ~~ any(@opened) {
            $fh = open $file, :a;
        } else {
            @opened.push($file);
            $fh = open $file, :w;
        }

        for @lines -> $line {
            $fh.print("$line\n");
        } 

        $fh.close;
    }
}

multi MAIN ( 'snip', Str $path, Str $name ) {
    my @blocks = get_blocks $path;
    for @blocks -> $block {
        my @lines = $block.lines;
        my ($lang, $file) = @lines.shift.words;

        if $file eq "#$name" {
            my $fh = open 'snippet', :w;
            for @lines -> $line {
                $fh.print("$line\n");
            } 
            $fh.close;
            
            shell "bash urynus_config $lang";
            unlink 'snippet';
            last;
        } 
    }
}

multi MAIN ( 'init' ) {
    my $fh = open 'urynus_config', :a;
    $fh.print('
#!/bin/bash 
case $1 in
    python) python3 snippet ;;
esac');
    $fh.close;
}
